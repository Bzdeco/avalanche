buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.4.RELEASE'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'application'

    group 'org.org.avalanche'
    version '1.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        // Logging
        implementation 'org.apache.logging.log4j:log4j-api:2.11.2'
        runtimeOnly 'org.apache.logging.log4j:log4j-core:2.11.2'

        // Lombok
        implementation 'org.projectlombok:lombok:1.18.6'
        annotationProcessor 'org.projectlombok:lombok:1.18.6'

        // JUnit 5
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.0-M1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.0-M1'
    }

    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
        }
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

dependencies {
    // Sub-projects
    implementation project(':las2etin')
    implementation project(':weather')

    // Guava
    implementation 'com.google.guava:guava:27.1-jre'
    
    // Solar positioning
    implementation 'net.e175.klaus:solarpositioning:0.0.9'
    
    // Math
    implementation 'org.apache.commons:commons-math3:3.6.1'
    
    // Tinfour
    implementation 'org.tinfour:Tinfour:2.0'

    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:3.14.1'

    // Spring
    implementation 'org.springframework.boot:spring-boot-starter:2.1.4.RELEASE'
}

jar {
    manifest {
        attributes('Main-Class': 'org.avalanche.Main',
                   'Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

application {
    mainClassName = 'org.avalanche.Main'
    applicationDefaultJvmArgs = ['-ea', '-Xmx2048m']
}